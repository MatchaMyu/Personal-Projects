; Multiboot v1 header + entry that calls C kernel_main
; - Entry point for the kernel when GRUB hands off control.
; - Runs in 32-bit protected mode. (Indicated by BITS 32)
; - Calls the C entry using "extern kernel_main"
; - Long term goal: Switch to long mode and jump to 64-bit entry

BITS 32

SECTION .multiboot
align 4
dd 0x1BADB002            ; magic
dd 0x00000000            ; flags (keep minimal)
dd -(0x1BADB002 + 0x00000000) ; checksum

SECTION .text
global _start
extern kernel_main

_start:
    cli
    lgdt [gdt_descriptor]

    ; call C code
    jmp 0x08:flush_cs

flush_cs:
    ; Reload data segment registers with our data selector (0x10)
    mov ax, 0x10
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    mov ss, ax

    ; Set up a simple stack (now using our known SS)
    mov esp, stack_top

    ; Call C kernel
    call kernel_main

.hang:
    hlt
    jmp .hang

SECTION .rodata
align 8
gdt_start:
    dq 0x0000000000000000        ; 0x00: null descriptor

    ; 0x08: code segment (base=0, limit=4GB), present, ring0, executable, readable
    dq 0x00CF9A000000FFFF

    ; 0x10: data segment (base=0, limit=4GB), present, ring0, writable
    dq 0x00CF92000000FFFF

gdt_end:

gdt_descriptor:
    dw gdt_end - gdt_start - 1   ; limit
    dd gdt_start                 ; base

SECTION .bss
align 16
stack_bottom:
    resb 16384            ; 16 KiB stack
align 16
stack_top:
